plugins {
    id 'java'
    id 'war'
}

group 'ru.clevertec.check'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.28'
}

test {
    useJUnitPlatform()
    jvmArgs '-Dnet.bytebuddy.experimental=true'
}

tasks.register('copyJarToRoot', Copy) {
    from('./build/libs/check-1.0-SNAPSHOT.jar')
    into('.')
    rename 'check-1.0-SNAPSHOT.jar', 'clevertec-check.jar'
    doNotTrackState('Should run every time. Root directory content cannot be cached.')
    mustRunAfter(':jar', ':compileTestJava', ':test')
}

war {
    archiveFileName = 'clevertec-check.war'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'ru.clevertec.check.CheckRunner',
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    finalizedBy('copyJarToRoot')
}

